buildscript {
    ext.kotlin_version = '1.2.30'

    repositories {
        mavenCentral()

        maven {
            credentials {
                username System.getenv('HAULMONT_REPOSITORY_USER') ?: 'cuba'
                password System.getenv('HAULMONT_REPOSITORY_PASSWORD') ?: 'cuba123'
            }
            url System.getenv('HAULMONT_REPOSITORY_URL') ?: 'https://repo.cuba-platform.com/content/groups/work'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

group 'com.haulmont'
version '1.0-SNAPSHOT'

def moduleName = "cuba.cli.main"

def velocityVersion = "1.7"
def jCommanderVersion = "1.72"
def jansiVersion = "1.17"
def guavaVersion = "21.0"
def slf4jVersion = "1.7.25"
def jlineVersion = "3.6.2"
def kodeinVersion = "5.0.0"

apply plugin: 'kotlin'
apply plugin: 'java-library'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 9
targetCompatibility = 9

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "com.beust:jcommander:$jCommanderVersion"
    implementation "org.jline:jline:$jlineVersion"
    implementation "org.fusesource.jansi:jansi:$jansiVersion"

    implementation "org.apache.velocity:velocity:$velocityVersion"

    implementation "com.google.guava:guava:$guavaVersion"

    implementation "org.slf4j:slf4j-simple:$slf4jVersion"

    implementation "org.kodein.di:kodein-di-generic-jvm:$kodeinVersion"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    destinationDir = compileJava.destinationDir
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.haulmont.cuba.cli.EntryPointKt'
    }
}